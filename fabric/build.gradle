plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

repositories {
    maven { url = "https://cursemaven.com" }
}

base {
    archivesName = "fabric-${mod_id}-${minecraft_version}"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    //RUNICLIB
    modImplementation "curse.maven:runiclib-880879:${fabric_runiclib}"
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
        datagen {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("../common/src/main/generated/")}"
            vmArg "-Dfabric-api.datagen.modid=urban_decor"

            runDir "build/datagen"
        }
    }
}

sourceSets.main.resources { srcDir project(":common").file('src/main/generated/').getAbsolutePath() }